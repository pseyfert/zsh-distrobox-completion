#compdef distrobox-generate-entry

_distrobox-generate-entry() {
    # expl is an internal zsh array that gets passed to _arguments bts
    local expl
    local -a options
    # Array of optargs to pass to _arguments for matching and completion
    options=(
        '(-h --help)'{-h,--help}'[show this message]'
        '(-a --all)'{-a,--all}'[perform for all distroboxes]'
        '(-d --delete)'{-d,--delete}'[delete the entry]'
        '(-i --icon)'{-i,--icon}'[specify a custom icon (default auto)]:icon path:(auto _files)'
        '(-r --root)'{-r,--root}'[perform on rootful distroboxes]'
        '(-v --verbose)'{-v,--verbose}'[show more verbosity]'
        '(-V --version)'{-V,--version}'[show version]'
    )
    _message -r "Create a desktop icon for a distrobox"
    _arguments \
      '*:containers:_distrobox_containers' \
      $options[@]
}

_distrobox-generate-entry
